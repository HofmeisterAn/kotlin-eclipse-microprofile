buildscript {
  ext.kotlin_version = '1.2.51'
  ext.junit_version = '5.2.0'

  repositories { jcenter() }

  dependencies { classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version" }
}

plugins { id 'com.diffplug.gradle.spotless' version '3.14.0' }

apply plugin: 'war'
apply plugin: 'kotlin'

sourceCompatibility = 10
targetCompatibility = 10

repositories { jcenter() }

sourceSets {
  main {
    kotlin { srcDirs += 'src/main/kotlin' }
    java { srcDirs += 'src/main/java' }
    resources { srcDirs += 'src/main/resources' }
  }
  test {
    kotlin { srcDirs += 'src/test/kotlin' }
    java { srcDirs += 'src/test/java' }
    resources {
      srcDirs += [
        'src/test/resources',
        'src/test/resources-payara-micro-managed'
      ]
    }
  }
}

configurations { payaraMicroManagedTest { extendsFrom testImplementation } }

dependencies {
  // Kotlin
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

  // Application
  implementation 'javax.ejb:javax.ejb-api:3.2.2'
  implementation 'org.eclipse.microprofile:microprofile:1.3'
  implementation 'org.hibernate:hibernate-core:5.3.4.Final'
  implementation 'com.h2database:h2:1.4.197'

  // JUnit
  testImplementation "junit:junit:4.12"
  testImplementation "org.junit.jupiter:junit-jupiter-params:$junit_version"
  testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
  testRuntimeOnly "org.junit.vintage:junit-vintage-engine:$junit_version"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"

  // Arquillian
  testImplementation 'org.jboss.shrinkwrap.resolver:shrinkwrap-resolver-impl-gradle:3.1.3'
  testImplementation 'org.jboss.shrinkwrap.resolver:shrinkwrap-resolver-gradle-depchain:3.1.3'
  testImplementation 'org.jboss.arquillian:arquillian-bom:1.4.0.Final'
  testImplementation 'org.jboss.arquillian.junit:arquillian-junit-container:1.4.0.Final'

  // Arquillian Payara
  testImplementation 'fish.payara.arquillian:arquillian-payara-micro-5-managed:1.0.Beta3'
}

test {
  testLogging { showStandardStreams = true }
  useJUnitPlatform { includeEngines 'junit-jupiter', 'junit-vintage' }
}

spotless {
  groovyGradle {
    target '*.gradle'
    greclipse()
    indentWithSpaces(2)
    endWithNewline()
    paddedCell()
  }
  kotlin {
    ktlint().userData([
      'indent_size'             : '2',
      'continuation_indent_size': '2',
      'insert_final_newline'    : 'true',
      'max_line_length'         : 'off'
    ])
  }
}

def payaraMicroJar = 'payara-micro-5.183-20180810.000318-70.jar'

/**
 * Starts the microprofile web app on payara micro.
 */
task run(type: JavaExec, dependsOn: war) {
  main = '-jar'

  jvmArgs = [
    '-Xdebug',
    '-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=5005'
    // '--add-modules=java.se.ee' This is necessary for Java 9 support. Remove this as soon as Payara Micro supports Java 9 and above.
  ]

  args = [
    payaraMicroJar,
    '--deploy',
    war.archivePath
  ]
}

/**
 * Runs the Arquillian tests based on Payara.
 */
task payaraMicroManagedTest(type: Test) {
  classpath = sourceSets.main.output + sourceSets.test.output + configurations.payaraMicroManagedTest

  systemProperties = [
    'arquillian.launch': 'payara-micro-managed'
  ]
}
