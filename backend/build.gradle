sourceSets {
  main {
    kotlin { srcDirs += 'src/main/kotlin' }
    java { srcDirs += 'src/main/java' }
    resources { srcDirs += 'src/main/resources' }
  }
  test {
    kotlin { srcDirs += 'src/test/kotlin' }
    java { srcDirs += 'src/test/java' }
    resources {
      srcDirs += [
        'src/test/resources',
        'src/test/resources-payara-micro-managed' // Add configurations for Payara micro tests here.
      ]
    }
  }
}

dependencies {
  // Application
  implementation 'org.eclipse.microprofile:microprofile:1.4'
  implementation 'org.bouncycastle:bcprov-jdk15on:1.60'
  implementation 'org.json:json:20180813'
  implementation 'org.hibernate:hibernate-core:5.3.7.Final'
  implementation 'com.h2database:h2:1.4.197'
  implementation 'com.auth0:java-jwt:3.4.1'

  compileOnly 'javax.ejb:javax.ejb-api:3.2.2'
  compileOnly 'javax.servlet:javax.servlet-api:4.0.1'

  // Arquillian
  testImplementation 'org.slf4j:slf4j-nop:1.7.25'
  testImplementation 'org.jboss.arquillian.junit:arquillian-junit-container:1.4.1.Final'
  testImplementation 'org.jboss.arquillian:arquillian-bom:1.4.1.Final'
  testImplementation 'org.jboss.shrinkwrap.resolver:shrinkwrap-resolver-gradle-depchain:3.1.3'
  testImplementation 'org.jboss.shrinkwrap.resolver:shrinkwrap-resolver-impl-gradle:3.1.3'

  // Arquillian Payara
  testImplementation 'fish.payara.arquillian:arquillian-payara-micro-5-managed:1.0.Beta3'
}

war { archiveName 'app.war' }

def payaraMicroJar = 'payara-micro-5.183.jar'

/**
 * Copies the front end assets to webapp.
 */
task copyFrontendAssets(type: Copy) {
  delete fileTree('src/main/webapp').matching { exclude 'WEB-INF', '**/.gitkeep' }

  from "$project.rootDir/frontend/dist"
  into 'src/main/webapp'
}

/**
 * Starts the MicroProfile web app on Payara micro with debug port enabled.
 */
task debug(type: JavaExec, dependsOn: war) {
  main = '-jar'

  jvmArgs = [
    '-Xdebug',
    '-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=5005'
  ]

  args = [
    payaraMicroJar,
    '--deploy',
    war.archivePath,
    '--nocluster',
    '--nohostaware'
  ]
}

/**
 * Starts the MicroProfile web app on Payara micro.
 */
task prod(type: JavaExec, dependsOn: [copyFrontendAssets, war]) {
  main = '-jar'

  args = [
    payaraMicroJar,
    '--deploy',
    war.archivePath,
    '--nocluster',
    '--nohostaware'
  ]
}

/**
 * Runs the Arquillian tests based on Payara micro.
 */
task payaraMicroManagedTest(type: Test) {
  systemProperties = [
    'arquillian.launch': 'payara-micro-managed'
  ]
}
